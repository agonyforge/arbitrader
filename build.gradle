buildscript {
    dependencies {
        classpath group: 'se.transmode.gradle', name: 'gradle-docker', version: '1.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'info.solidsoft.pitest' version '1.5.1'
    id 'org.owasp.dependencycheck' version '5.3.2.1'
    id 'org.kordamp.gradle.stats' version '0.2.2'
}

apply plugin: 'docker'

group 'com.r307'
version '0.8.4-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

configurations.compile {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
}

dependencies {
    compile group: 'javax.inject', name: 'javax.inject', version: '1'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter'

    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

    compile group: 'org.knowm.xchange', name: 'xchange-quoine', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-kraken', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-bitflyer', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-coinbasepro', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-cexio', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-poloniex', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-gemini', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-bitstamp', version: '4.4.2'
    compile group: 'org.knowm.xchange', name: 'xchange-cobinhood', version: '4.4.2'

    compile group: 'com.github.seratch', name: 'jslack', version: '1.1.6'
    compile group: 'javax.websocket', name: 'javax.websocket-api', version: '1.1'
    compile group: 'org.glassfish.tyrus.bundles', name: 'tyrus-standalone-client', version: '1.17'

    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
}

task buildDocker(type: Docker) {
    push = false
    applicationName = jar.getBaseName()
    dockerfile = file('src/main/docker/local/Dockerfile')
    tagVersion = "latest"
    tag = "scionaltera/${applicationName}"

    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

build.dependsOn project.tasks.buildDocker

pitest {
    excludedMethods = [
            'hashCode',
            'equals',
            'toString'
    ]
    timestampedReports = false
}

dependencyCheck {
    suppressionFile "codequality/cve-suppressions.xml"
}

check.dependsOn project.tasks.pitest, project.tasks.dependencyCheckAnalyze, project.tasks.stats

project.tasks.pitest.mustRunAfter test
